buildscript {
    repositories {
        jcenter()
        maven {
            name 'jungletree-snapshots'
            url 'https://repo.jungletree.org/repository/jungletree-snapshots/'
        }
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.1'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

def jungleTreeNexusUsername = hasProperty('jungleTreeNexusUsername') ? jungleTreeNexusUsername : System.getenv('jungleTreeNexusUsername')
def jungleTreeNexusPassword = hasProperty('jungleTreeNexusPassword') ? jungleTreeNexusPassword : System.getenv('jungleTreeNexusPassword')

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'org.junit.platform.gradle.plugin'

    group 'org.jungletree'
    version '0.0.1-SNAPSHOT'

    sourceCompatibility = 9
    targetCompatibility = 9

    ext.junit4Version        = '4.12'
    ext.junitVintageVersion  = '4.12.1'
    ext.junitPlatformVersion = '1.0.1'
    ext.junitJupiterVersion  = '5.0.1'
    ext.log4jVersion         = '2.9.0'

    repositories {
        jcenter()
        maven {
            name 'jungletree-snapshots'
            url 'https://repo.jungletree.org/repository/jungletree-snapshots/'
        }
    }

    junitPlatform {
        // platformVersion '1.0.1'
        filters {
            engines {
                // include 'junit-jupiter', 'junit-vintage'
                // exclude 'custom-engine'
            }
            tags {
                // include 'fast'
                exclude 'slow'
            }
        }
        logManager 'org.apache.logging.log4j.jul.LogManager'
    }

    dependencies {
        // JUnit Jupiter API and TestEngine implementation
        testCompile("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
        testRuntime("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")

        // If you also want to support JUnit 3 and JUnit 4 tests
        testCompile("junit:junit:${junit4Version}")
        testRuntime("org.junit.vintage:junit-vintage-engine:${junitVintageVersion}")

        // To avoid compiler warnings about @API annotations in JUnit code
        testCompileOnly('org.apiguardian:apiguardian-api:1.0.0')

        // To use Log4J's LogManager
        testRuntime("org.apache.logging.log4j:log4j-core:${log4jVersion}")
        testRuntime("org.apache.logging.log4j:log4j-jul:${log4jVersion}")

        // Only needed to run tests in an (IntelliJ) IDE(A) that bundles an older version
        testRuntime("org.junit.platform:junit-platform-launcher:${junitPlatformVersion}")
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

subprojects {
    afterEvaluate {
        compileJava {
            inputs.property("moduleName", moduleName)
            doFirst {
                options.compilerArgs = [
                        '--module-path', classpath.asPath,
                ]
                classpath = files()
            }
        }

        compileTestJava {
            inputs.property("moduleName", moduleName)
            doFirst {
                options.compilerArgs = [
                        '--module-path', classpath.asPath,
                        '--add-modules', 'junit',
                        '--add-reads', "$moduleName=junit",
                        '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
                ]
                classpath = files()
            }
        }

        test {
            inputs.property("moduleName", moduleName)
            doFirst {
                jvmArgs = [
                        '--module-path', classpath.asPath,
                        '--add-modules', 'ALL-MODULE-PATH',
                        '--add-reads', "$moduleName=junit",
                        '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath,
                ]
                classpath = files()
            }
        }

        publishing {
            repositories {
                maven {
                    credentials {
                        username jungleTreeNexusUsername
                        password jungleTreeNexusPassword
                    }
                    if (version.endsWith('-SNAPSHOT')) {
                        url "https://repo.jungletree.org/repository/jungletree-snapshots"
                    } else {
                        url "https://repo.jungletree.org/repository/jungletree-releases"
                    }
                }
            }
            publications {
                mavenJava(MavenPublication) {
                    groupId group
                    artifactId "${moduleName.substring("rainforest.".length())}"
                    version version

                    from components.java
                }
            }
        }
    }
}
